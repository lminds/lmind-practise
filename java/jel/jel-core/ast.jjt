options {
  STATIC=false;
  MULTI=false;
  NODE_PREFIX="Jel";
  NODE_PACKAGE="org.lmind.jel.core.ast";
  NODE_FACTORY="JelNodeFactory";
  NODE_CLASS="JelNode";
  BUILD_NODE_FILES=false;
}

PARSER_BEGIN(JelParser)
package org.lmind.jel.core.ast;

/** Simple brace matcher. */
public class JelParser {


}

PARSER_END(JelParser)

SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
}

TOKEN : /* RESERVED WORDS AND LITERALS */
{
  < ABSTRACT: "abstract" >
| < BOOLEAN: "boolean" >
| < BREAK: "break" >
| < BYTE: "byte" >
| < CASE: "case" >
| < CATCH: "catch" >
| < CHAR: "char" >
| < CLASS: "class" >
| < CONST: "const" >
| < CONTINUE: "continue" >
| < _DEFAULT: "default" >
| < DO: "do" >
| < DOUBLE: "double" >
| < ELSE: "else" >
| < EXTENDS: "extends" >
| < FALSE: "false" >
| < FINAL: "final" >
| < FINALLY: "finally" >
| < FLOAT: "float" >
| < FOR: "for" >
| < GOTO: "goto" >
| < IF: "if" >
| < IMPLEMENTS: "implements" >
| < IMPORT: "import" >
| < INSTANCEOF: "instanceof" >
| < INT: "int" >
| < INTERFACE: "interface" >
| < LONG: "long" >
| < NATIVE: "native" >
| < NEW: "new" >
| < NULL: "null" >
| < PACKAGE: "package">
| < PRIVATE: "private" >
| < PROTECTED: "protected" >
| < PUBLIC: "public" >
| < RETURN: "return" >
| < SHORT: "short" >
| < STATIC: "static" >
| < SUPER: "super" >
| < SWITCH: "switch" >
| < SYNCHRONIZED: "synchronized" >
| < THIS: "this" >
| < THROW: "throw" >
| < THROWS: "throws" >
| < TRANSIENT: "transient" >
| < TRUE: "true" >
| < TRY: "try" >
| < VOID: "void" >
| < VOLATILE: "volatile" >
| < WHILE: "while" >
}

TOKEN : /* LITERALS */
{
  < INTEGER_LITERAL:
        <DECIMAL_LITERAL> (["l","L"])?
      | <HEX_LITERAL> (["l","L"])?
  >
|
  < #DECIMAL_LITERAL: "0" | (["1"-"9"] (["0"-"9"])*) >
|
  < #HEX_LITERAL: "0" ["x","X"] (["0"-"9","a"-"f","A"-"F"])+ >
|
  < FLOATING_POINT_LITERAL:
        (["0"-"9"])+ "." (["0"-"9"])* (["f","F","d","D"])?
      | "." (["0"-"9"])+  (["f","F","d","D"])?
  >
|
  < STRING_LITERAL:
      "\""
      (   (~["\"","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
			| ["u"] ["0"-"9"] ( ["0"-"9"] )*
            )
          )
      )*
      "\""
  >
}

TOKEN : /* IDENTIFIERS */
{
  < IDENTIFIER: <LETTER> (<LETTER>|<DIGIT>)* >
|
  < #LETTER:
      [
       "\u0024",
       "\u0041"-"\u005a",
       "\u005f",
       "\u0061"-"\u007a"
      ]
  >
|
  < #DIGIT:
      [
       "\u0030"-"\u0039"
      ]
  >
}

void BooleanLiteral() :
{
  Token t;
}
{
  t="true"
  {
    jjtThis.setImage(t.image);
  }
|
  t="false"
  {
    jjtThis.setImage(t.image);
  }
}

void NullLiteral() :
{}
{
  "null"
}

void StringLiteral() :
{
  Token t;
}
{
  t=<STRING_LITERAL>
  {
    jjtThis.setImage(t.image);
  }
}

void NumberLiteral() :
{
  Token t;
}
{
  t=<INTEGER_LITERAL>
  {
    jjtThis.setImage(t.image);
  }
|
  t=<FLOATING_POINT_LITERAL>
  {
    jjtThis.setImage(t.image);
  }
}

void Literal() #void :
{}
{
  NumberLiteral()
|
  StringLiteral()
|
  BooleanLiteral()
|
  NullLiteral()
}

void Identifier() :
{
  Token t;
}
{
  t=<IDENTIFIER>
  {
    jjtThis.setImage(t.image);
  }
}


/** Root production. */
JelNode Expression() :
{}
{
  ExpressionTerm() <EOF>
  { return jjtThis; }
}

/*
 * Expression syntax follows.
 */

void ExpressionTerm() #void :
{}
{
  TernaryExpression()
}

void TernaryExpression() #void :
{}
{
  ConditionalOrExpression() [ "?" ExpressionTerm() ":" TernaryExpression() #TernaryExpression(3) ]
}

void ConditionalOrExpression() #void :
{}
{
  ConditionalAndExpression()
  (
    "||" ConditionalAndExpression() #OrExpression(2)
  )*
}

void ConditionalAndExpression() #void :
{}
{
  EqualityExpression()
  (
    "&&" EqualityExpression() #AndExpression(2)
  )*
}

void EqualityExpression() #void :
{}
{
  RelationalExpression()
  (
    "==" RelationalExpression() #EqualsExpression(2)
	|
	"!=" RelationalExpression() #NotEqualsExpression(2)
  )*
}

void RelationalExpression() #void :
{}
{
  AdditiveExpression()
  (
    "<" AdditiveExpression() #LesserExpression(2)
	|
	">" AdditiveExpression() #GreaterExpression(2)
	|
	"<=" AdditiveExpression() #LesserEqualsExpression(2)
	|
	">=" AdditiveExpression() #GreaterEqualsExpression(2)
  )*
}

void AdditiveExpression() #void :
{}
{
  MultiplicativeExpression()
  (
    "+" MultiplicativeExpression() #AddExpression(2)
	|
	"-" MultiplicativeExpression() #SubExpression(2)
  )*
}

void MultiplicativeExpression() #void :
{}
{
  NegateExpression()
  (
     "*" NegateExpression() #MultiplyExpression(2)
	 | 
	 "/" NegateExpression() #DivisionExpression(2)
	 | 
	 "%" NegateExpression() #ModExpression(2)
  )*
}

void NegateExpression() #void :
{}
{
  "+" NegateExpression()
  | 
  "-" NegateExpression()  #NegateExpression 
  |
  UnaryExpressionNot()
}

void UnaryExpressionNot() #void :
{}
{
  "!"  UnaryExpressionNot() #UnaryExpressionNot(1)
|
  InvocationExpression()
}

void InvocationExpression() #void :
{
  Token t;
}
{
  PrimaryExpression()
  (
    "." Identifier() #PropertyReadExpression(2)
	|
	"[" ExpressionTerm() "]" #IndexPropertyReadExpression(2)
	|
	"(" (CallParametersExpression()) ")" #CallExpression(2)
  )*
}

void CallParametersExpression() :
{}
{
  (ExpressionTerm() ( "," ExpressionTerm() )*)?
}

void PrimaryExpression() #void :
{}
{
  "(" ExpressionTerm() ")"
|
  Reference()
|
  Literal()
}

// 对象引用
void Reference() :
{
  Token t;
}
{
  t=<IDENTIFIER>
  {
    jjtThis.setImage(t.image);
  }
}



